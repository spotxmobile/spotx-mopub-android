buildscript {
    repositories {
      jcenter()
    }
    dependencies {
      classpath 'com.android.tools.build:gradle:1.1.3'
      classpath 'com.github.dcendents:android-maven-plugin:1.2'
      classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.1"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'signing'

def KEYSTORE = System.getenv("KEYSTORE");
def KEYSTORE_PASS = System.getenv("KEYSTORE_PASS")
def KEYSTORE_KEY_PASS = System.getenv("KEYSTORE_KEY_PASS")
def GPG_KEYRING = System.getenv("GPG_KEYRING");
def GPG_KEY = System.getenv("GPG_KEY")
def GPG_KEY_PASS = System.getenv("GPG_KEY_PASS")
def SONATYPE_USER = System.getenv("SONATYPE_USER")
def SONATYPE_PASS = System.getenv("SONATYPE_PASS")

group = 'com.spotxchange'
version = '2.0.0'

println "######################################################"
println "Group: " + group
println "ArtifactId: " + rootProject.name
println "Version: " + version
println "KEYSTORE:" + KEYSTORE
println "GPG KEYRING:" + GPG_KEYRING
println "######################################################"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile('com.mopub:mopub-sdk:3.11.0@aar') {
        transitive = true
    }
    compile 'com.spotxchange:spotx-sdk-android:2.0.0@aar'
    compile 'com.google.android.gms:play-services-ads:7.0.0'
}

android {
    defaultPublishConfig "release"
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName version
    }
    buildTypes {
        debug {

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        release {
            if (KEYSTORE) {
                storeFile file(KEYSTORE)
                storePassword KEYSTORE_PASS
                keyAlias "spotx"
                keyPassword KEYSTORE_KEY_PASS
            }
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: SONATYPE_USER, password: SONATYPE_PASS)
            }

            pom.project {
               name 'spotx-mopub-android'
               packaging 'aar'
               description 'MoPub adapter for SpotXchange'
               url 'http://www.spotxchange.com/'

              licenses {
                   license {
                       name 'Apache'
                       url 'http://www.apache.org/licenses/LICENSE-2.0'
                   }
               }

               scm {
                 url 'https://github.com/spotxmobile/spotx-mopub-android'
               }

               developers {
                   developer {
                       id 'spotxmobile'
                       name 'SpotXchange Inc.'
                   }
               }
           }
        }
    }
}

allprojects { ext."signing.secretKeyRingFile" = GPG_KEYRING }
allprojects { ext."signing.keyId" = GPG_KEY }
allprojects { ext."signing.password" = GPG_KEY_PASS }

signing {
  sign configurations.archives
}

nexusStaging {
  packageGroup = 'com.spotxchange'
  stagingProfileId = '42ae9b7711a5f'
}